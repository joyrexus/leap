<!DOCTYPE html>
<meta charset="utf-8">
<script src="lib/coffee-script.js"></script>
<script src="lib/svg.js"></script>
<style>
  body {
    margin-top: 25px;
    margin-left: 50px;
    font-family: "Helvetica Neue", sans-serif;
    font-weight: 100;
    font-size: 75px;
    color: #777;
    -webkit-font-smoothing: antialiased;
  }
  svg {
    margin-top: 25px;
    margin-bottom: 25px;
  }
  #file {
    color: steelblue;
  }
  #file:hover {
    color: orange;
    cursor: pointer;
  }
  #value {
    font-weight: 300;
  }
  .border {
    fill: none;
    stroke: #777;
    stroke-width: 2;
  }
  input {
    display: none;
  }
</style>
<body>
  <input type="file" id="chooser" />
  <div id="file">Click and choose sample</div>
  <div id="time">Time</div>
  <div id="value">Value</div>
  <div id="canvas"></div>
  <div id="selection">
    <div id="begin">Begin</div>
    <div id="end">End</div>
  </div>

<script type="text/coffeescript">
  $ = (selection) -> document.querySelector selection

  w = 960         # width
  h = 800         # height
  axis = h/2      # place x-axis halfway down
  tick = 0        # current tick count along x-axis of plot
  clicks = 0      # number of clicks on plot (0, 1, or 2)
  skip = false    # uncaptured data to skip?
  draw = 'canvas' # global variable for SVG canvas
  points = []     # array of data points on plot

  colors =        # color conventions
    position: 'steelblue'
    velocity: '#777'
    highlight: 'orange'

  selection =     # range of selected data points
    active: false
    begin: null
    end: null

  # highlight and get info on the data point under the cursor
  focus = ->
    @stroke colors.highlight
    time.innerHTML = @data 'time'
    value.innerHTML = @data 'value'

  # reset selection
  reset= ->
    for p in points[selection.begin..selection.end]
      p.stroke(colors.velocity)
      p.data('selected', false)
    selection.active = false
    selection.end = null
    end.innerHTML = 'End'

  # click handler for each data point
  clickSelector = -> 
    clicks += 1
    if clicks is 1                        # first click to begin
      reset()
      selection.begin = @data 'index'
      begin.innerHTML = @data 'time'
    else                                  # second click to end
      selection.active = true
      selection.end = @data 'index'
      end.innerHTML = @data 'time'
      for p in points[selection.begin..selection.end]
        p.stroke(colors.highlight)
        p.data('selected', true)
      clicks = 0                          # reset count

  drawLine = (time, value, tick, color='steelblue') ->
      draw.line(tick, axis, tick, axis - value)
        .stroke(color)
        .data('time', time)
        .data('value', value)
        .mouseover(focus)
        .mouseout(-> (@stroke do -> color) if not @data 'selected')

  mockLine =  # null line object with mock attrs
    stroke: -> @
    data: (key, value) -> 
      @[key] = value
      @

  # plot the position and velocity of the y-coord in each frame of data
  plot = (data) -> 
    start = data[0].timestamp
    w = data.length if data.length > w
    draw = SVG('canvas').size(w, h)
    draw.rect(w, h).attr("class", "border")

    # iterate over each frame/data-point `d`
    for i, d of data              # `i` is the index of each data point
      time = (d.timestamp - start) / 1000000
      position = d.left.pos.y     # y-coord position of left hand
      velocity = d.left.vel.y     # y-coord velocity of left hand
      if position
        skip = false              # don't skip next empty value
        tick += 1                 # move over 1 tick on axis
        drawLine(time, position, tick)
        v = drawLine(time, velocity, tick, color="#777")
          .click(clickSelector)   # handle click-selection
          .opacity(.8)
          .data('index', i)       # add index data for making selections
        points.push v             # add data point for velocity
      else
        tick += 10 if not skip    # create empty space
        skip = true               # skip additional empty values
        # points.push mockLine

  velocity = (d) -> d.left.vel.y  # y-coord position of left hand
  position = (d) -> d.left.pos.y  # y-coord velocity of left hand

  # load handler invoked on change event
  load = -> 
    File = @files[0]
    return if not File.type.match '\.json$'
    file.textContent = File.name
    reader = new FileReader()
    reader.onload = (file) ->
      data = JSON.parse @result
      plot data, velocity
    reader.readAsText File

  # click and choose a file to load
  chooser = $('#chooser')
  chooser.addEventListener('change', load)
  $('#file').addEventListener('click', -> chooser.click())
<script>
